class DislinCanvas;

classtype int[3];

use DislinSystem: all except{color};
use DislinPage: all except {getSize};
use Structures: all;

export all;

/******************************************************************************/

int getWinId( DislinCanvas & disp)
{
  tmp = from_DislinCanvas( disp);
  disp = to_DislinCanvas( tmp);
  return(tmp[[0]]);
}

int[2] getSize( DislinCanvas & disp)
{
  tmp = from_DislinCanvas( disp);
  disp = to_DislinCanvas( tmp);
  return(drop( [1], tmp));
}

DislinCanvas createPdfFile()
{
  return( createFile( "pdf"));
}

DislinCanvas createFile( string format)
{
  return( createCanvas( createPage(), format, [853,603]));
}

DislinCanvas createDisplay()
{
  return( createCanvas( createPage(), "xwin", [853,603]));
}

DislinCanvas createDisplay( DislinPage page)
{
  return( createCanvas( page, "xwin", [800,600]));
}

DislinCanvas createDisplay( int[2] size)
{
  return( createCanvas( createPage(), "xwin", size));
}

DislinCanvas createDisplay( DislinPage page, int[2] size)
{
  return( createCanvas( page, "xwin", size));
}

void registerDisplay( int winid)
{
  status = getGlobalStatus();
  status[ winid] = true;
  setGlobalStatus( status );
}

void unregisterDisplay( DislinCanvas disp)
{
  winid = getWinId( disp);
  status = getGlobalStatus();
  status[ winid] = false;
  setGlobalStatus( status );
}

DislinCanvas createCanvas( DislinPage page, string format, int[2] size)
{
  status = getGlobalStatus();
  if( !any(status) ) {
    metafl( format);
    if( sizeNormed( page)) {
      setpag( getNorm( page));
    } else {
      w,h = DislinPage::getSize( page);
      page( w, h);
    }
    winsiz( size[[0]], size[[1]]);
    disini();
    if( hasBorder( page)) {
      pagera();
    }
    status[1] = true;
    winid = 1;
  } else {
    if( format == "xwin") {
      winid = 1;
      while( status[winid] && (winid < 9)) {
        winid++;
      }
      if( winid < 9) {
        opnwin( winid);
        winsiz( size[[0]], size[[1]]);
        status[winid] = true;
      } else {
        /* cannot open more than 8 windows at a time! */
        winid = 0;
      }
    } else {
      /* only one non-X11 canvas at a time! */
      winid = 0;
    }
  }
  setGlobalStatus( status );

  return( to_DislinCanvas( [winid, size[[0]], size[[1]]]));
}


void activateDisplay( DislinCanvas & disp)
{
  winid = getWinId( disp);
  selwin( winid);
}

void clearDisplay( DislinCanvas &disp)
{
  activateDisplay( disp);
  erase();
}

void destroyDisplay( DislinCanvas disp)
{
  winid = getWinId( disp);
  if( winid > 1) {
    clswin( winid);
  }
  status = getGlobalStatus();
  status[winid] = false;
  if( !any(status) ) {
    disfin();
  } 
}

